plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

group 'com.shade'
version '0.1.1'

repositories {
    mavenCentral()
}

def getCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

buildConfig {
    buildConfigField('String', 'APP_TITLE', "\"Decima Explorer\"")
    buildConfigField('String', 'APP_VERSION', "\"${project.version}\"")
    buildConfigField('String', 'BUILD_COMMIT', "\"${getCommitHash()}\"")
    buildConfigField('long', 'BUILD_TIME', "${System.currentTimeMillis()}L")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'com.shade.decima.ui.Application'
}

dependencies {
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.10.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'

    // https://mvnrepository.com/artifact/org.reflections/reflections
    implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'

    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.2'

    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j-impl
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.17.2'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation group: 'com.miglayout', name: 'miglayout-swing', version: '11.0'

    // https://mvnrepository.com/artifact/com.formdev
    implementation group: 'com.formdev', name: 'flatlaf', version: '2.0.2'
    implementation group: 'com.formdev', name: 'flatlaf-extras', version: '2.0.2'

    // https://mvnrepository.com/artifact/org.junit.jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.1'
}
